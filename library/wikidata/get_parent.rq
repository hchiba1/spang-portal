# @title Get parents of a specific person
# @param arg1=wd:Q171977 

SELECT ?parent ?gender_en ?label_en ?descr_en ?birth_date ?death_date ?img
WHERE {
    # $arg1 wdt:P22|wdt:P25 ?parent .
    ?parent wdt:P40 $arg1 .
    OPTIONAL {
        ?parent wdt:P21/rdfs:label ?gender_en .
        FILTER (lang(?gender_en) = "en")
    }
    OPTIONAL {
        ?parent rdfs:label ?label_en .
        FILTER (lang(?label_en) = "en")
    }
    OPTIONAL {
        ?parent schema:description ?descr_en .
        FILTER (lang(?descr_en) = "en")
    }
    OPTIONAL {
        ?parent wdt:P569 ?birth_date .
    }
    OPTIONAL {
        ?parent wdt:P570 ?death_date .
    }
    OPTIONAL {
        ?parent wdt:P18 ?img .
    }
}

